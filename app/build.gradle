if (!isLibrary.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        if (!isLibrary.toBoolean()) {
            applicationId "com.moko.bxp.nordic"
        }
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 2
        versionName "1.0.1"
    }

    buildTypes {
        release {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
        debug {
            if (!isLibrary.toBoolean()) {
                buildConfigField("boolean", "IS_LIBRARY", "false")
            } else {
                buildConfigField("boolean", "IS_LIBRARY", "true")
            }
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (!isLibrary.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }
    if (!isLibrary.toBoolean()) {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def formatedDate = new Date().format("yyyyMMddHHmm")
                    def apkFileName = "BXP-NORDIC_${defaultConfig.versionName}_${formatedDate}.apk"
                    if (outputFile.name.contains('debug')) {
                        apkFileName = "BXP-NORDIC_${defaultConfig.versionName}_${formatedDate}_debug.apk"
                    }
                    outputFileName = apkFileName
                }
            }
        }
    } else {
        libraryVariants.all { variant ->
            if (variant.buildType.name == "release") {
                // 修改aar名称
                variant.outputs.all { output ->
                    def formatedDate = new Date().format("yyyyMMddHHmm")
                    outputFileName = "BXP-NORDIC_${android.defaultConfig.versionName}_${formatedDate}.aar"
                }
                // 复制aar到指定目录
                variant.assemble.doLast {
                    variant.outputs.all { output ->
                        // 输出目录在：项目目录/build/outputs/aar/
                        def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar"].join(File.separator)
                        copy {
                            from output.outputFile
                            into outputPath
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    // Butter Knife
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.support:cardview-v7:29.0.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    implementation 'com.github.GrenderG:Toasty:1.4.1'
    implementation 'cn.carbswang.android:NumberPickerView:1.1.1'
    if (!isLibrary.toBoolean()) {
        implementation project(path: ':mokosupport')
    } else {
        implementation files('libs/mokoSupport.jar')
        provided files('libs/mokoBleLib.jar')
        implementation 'no.nordicsemi.android.support.v18:scanner:1.4.3'
        implementation 'no.nordicsemi.android:dfu:0.6.2'
        implementation 'org.greenrobot:eventbus:3.2.0'
        implementation 'com.elvishew:xlog:1.6.1'
        implementation 'no.nordicsemi.android:ble:2.2.4'
    }
}
